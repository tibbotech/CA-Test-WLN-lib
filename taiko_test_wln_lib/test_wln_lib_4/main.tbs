include "global.tbh"

'--------------------------------------------------------------------
dim tcp_sock as byte
dim rescan_tmr as byte

'====================================================================
sub on_sys_init()
	wln.ip="192.168.1.50"						'<----- set suitable IP here
	
	stg_start()


	if connect_to_ap(AP_NAME,AP_SECURITY,AP_PASSWORD,PL_WLN_DOMAIN_FCC)<>WLN_STATUS_OK then
		sys.halt
	end if

	'configure loopback socket
	tcp_sock=sock_get("TCP")
	sock.num=tcp_sock
	sock.rxbuffrq(1)
	sock.txbuffrq(1)
	sys.buffalloc
	sock.protocol=PL_SOCK_PROTOCOL_TCP
	sock.targetinterface=PL_SOCK_INTERFACE_WLN
	sock.targetip="192.168.1.92"				'<----- set suitable target IP here
	sock.targetport=1000						'<----- set suitable target port here
end sub

'--------------------------------------------------------------------
sub on_sys_timer()
	wln_proc_timer()
	
	if wln_check_association()=PL_WLN_ASSOCIATED then
		'this code ensures persistent connection to the target IP
		sock.num=tcp_sock
		if sock.statesimple=PL_SSTS_CLOSED then
			sock.connect
		end if

		'this code handles access point roaming
		if rescan_tmr>0 then
			rescan_tmr=rescan_tmr-1
			if rescan_tmr=0 then
				rescan_tmr=RESCAN_PERIOD
				if wln.rssi<=RSSI_THRESHOLD then

					#if SRC_LIB_VER < &h020003
						wln_rescan(AP_NAME) 
					#endif

					#if SRC_LIB_VER >= &h020003
						wln_rescan()
					#endif
				
				end if
			end if
		end if
	else
		rescan_tmr=RESCAN_PERIOD
	end if
end sub

'--------------------------------------------------------------------
sub on_sock_data_arrival()
	wln_proc_data()

	'loopback tcp data
	if sock.num=tcp_sock then
		sock.setdata(sock.getdata(sock.txfree))
		sock.send
	end if
end sub

'--------------------------------------------------------------------
sub on_wln_task_complete(completed_task as pl_wln_tasks)
	wln_proc_task_complete(completed_task)
end sub

'--------------------------------------------------------------------
sub on_wln_event(wln_event as pl_wln_events)
	wln_proc_event(wln_event)
end sub
